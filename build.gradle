plugins {
    id "java-library"
    id "maven-publish"
    id "xyz.jpenilla.run-paper" version "2.2.2"
}

group = 'com.dianlemel.huc'
version = '1.0'

repositories {
    mavenCentral()
    maven { // For Spigot API
        name "spigotmc"
        url "https://hub.spigotmc.org/nexus/content/groups/public/"
        content {
            includeGroup "org.bukkit"
            includeGroup "org.spigotmc"
            includeGroup "net.md-5"
        }
    }
    maven { // For PaperLib
        name "papermc"
        url "https://repo.papermc.io/repository/maven-public/"
        content {
            includeGroup "io.papermc"
        }
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:1.20.1-R0.1-SNAPSHOT"
}

tasks.named("runServer") {
    def mcVersion = "1.20.1"
    minecraftVersion mcVersion
    runDirectory rootProject.file("run/$mcVersion")
    javaLauncher = javaToolchains.launcherFor {
        languageVersion = JavaLanguageVersion.of(17)
    }
    jvmArgs += [ "-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=25566" ]
}